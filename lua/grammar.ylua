%file "lua/parser"


// Conflict resolvers, since the approximation of Lua we're parsing is not LL(1) (but is LL(2))
%resolve field Name {: function(self, tokens)
  if tokens[2] == 'EQ' then
    return self:go 'assign'
  else
    return self:go 'exp'
  end
end :}

// resolve conflict for parlist := (Name ',')+ Name; based on tokens[2] being , or not
%resolve parlist'star#1 Name {: function(self, tokens)
  if tokens[2] == 'COMMA' then
    return self:go 'namelist'
  else
    return self:go ''
  end
end :}

%resolve stat'group#2 Name {: function(self, tokens)
  if tokens[2] == 'EQ' then
    return self:go 'forcounter'
  else
    return self:go 'foreach'
  end
end :}

// Hack to get around the fact that one branch of this is always
// "dead" states that would get killed later on anyways
%resolve assignment_or_call'star#1 '(' {: function(self, tokens)
  -- always reduce to call
  return self:go 'call'
end :}

%quote '(' LPAREN
%quote ',' COMMA
%quote ':' COLON
%quote '[' LBRACK
%quote ']' RBRACK
%quote '.' PERIOD
%quote '~=' NOTEQ
%quote '==' EQEQ
%quote '>=' GE
%quote '>' GT
%quote '<=' LE
%quote '<' LT
%quote '..' CONCAT
%quote '%' MOD
%quote '^' POW
%quote '/' DIV
%quote '*' MUL
%quote '-' MIN
%quote '+' PLUS
%quote '=' EQ
%quote '#' HASH
%quote ')' RPAREN
%quote ';' SEMICOLON
%quote '...' DOTS
%quote '{' LBRACE
%quote '}' RBRACE
%quote '::' QUAD
%quote 'function' FUNCTION
%quote 'true' TRUE
%quote 'false' FALSE
%quote 'nil' NIL
%quote 'or' OR
%quote 'and' AND
%quote 'return' RETURN
%quote 'local' LOCAL
%quote 'for' FOR
%quote 'in' IN
%quote 'do' DO
%quote 'end' END
%quote 'if' IF
%quote 'not' NOT
%quote 'then' THEN
%quote 'else' ELSE
%quote 'elseif' ELSEIF
%quote 'repeat' REPEAT
%quote 'until' UNTIL
%quote 'while' WHILE
%quote 'goto' GOTO
%quote 'break' BREAK

// Character classes of tokens
%production Name "class of valid identifiers"
%production String "class of valid strings"
%production Number "class of valid numbers"

%production FUNCTION
%production EQ
%production COMMA
%production QUAD
%production PERIOD
%production LPAREN
%production RPAREN
%production END
%production SEMICOLON
%production LBRACE
%production RBRACE
%production OR
%production AND
%production NOTEQ
%production EQEQ
%production GE
%production GT
%production LE
%production LT
%production CONCAT
%production MOD
%production POW
%production DIV
%production MUL
%production MIN
%production PLUS
%production LBRACK
%production RBRACK
%production LOCAL
%production FOR
%production IF
%production THEN
%production REPEAT
%production UNTIL
%production WHILE
%production DO
%production GOTO
%production BREAK
%production IN
%production RETURN
%production DOTS
%production TRUE
%production FALSE
%production NIL
%production ELSE
%production ELSEIF
%production HASH
%production NOT
%production COLON

// A lua file is just a block
root := $block;

block := $stat* $retstat?;

stat := ';' | 
//  $varlist '=' $explist | 
//  $functioncall | // ( conflicts with var's (
  $assignment_or_call |
  $label | 
  'break' | 
  'goto' Name | 
  'do' $block 'end' | 
  'while' $exp 'do' $block 'end' | 
  'repeat' $block 'until' $exp | 
  'if' $exp 'then' $block ('elseif' $exp 'then' $block;)* ('else' $block;)? 'end' | 
  'for' (
      <forcounter> Name '=' $exp ',' $exp (',' $exp;)? 'do' $block 'end' | 
      <foreach> $namelist 'in' $explist 'do' $block 'end';) | 
  'function' $funcname $funcbody | 
  'local' ('function' Name $funcbody | $namelist ('=' $explist;)?;);

retstat := 'return' $explist? ';'?;

label := '::' Name '::';

funcname := Name ('.' Name;)* (':' Name;)?;

namelist := Name (',' Name;)*;

explist := $exp (',' $exp;)*;

exp := ('nil' | 'false' | 'true' | Number | String | '...' | $functiondef | 
  $suffixedexp | $tableconstructor | $unop $exp;) $binop $exp;

suffixedexp := $primaryexp ('.' Name | '[' $exp ']' | ':' Name $args | $args;)*;
primaryexp := Name | '(' $exp ')';

// %eps is func, the other 2 are assignments
// let's rule out the possibility of f() = x
assignment_or_call := $primaryexp ('.' Name | '[' $exp ']' | ':' Name $args | <call> $args;)* $assignment?;
assignment := ',' $suffixedexp $assignment | '=' $explist;

// prefixexp := $var | $functioncall | '(' $exp ')';

// functioncall := $prefixexp ($args | ':' Name $args;);

// varlist := $var (',' $var;)*;

// var := Name | $prefixexp ('[' $exp ']' | '.' Name;);

args := '(' $explist? ')' | $tableconstructor | String;

functiondef := 'function' $funcbody;

funcbody := '(' $parlist? ')' $block 'end';

parlist := (<namelist> Name ',';)+ (Name | '...';) | '...';

// TODO: ensure that only the last $field has an optional $fieldsep
tableconstructor := '{' $field* '}';

field := '[' $exp ']' '=' $exp $fieldsep? | <assign> Name '=' $exp $fieldsep? | <exp> $exp $fieldsep?;

fieldsep := ',' | ';';

binop := '+' | '-' | '*' | '/' | '^' | '%' | '..' | 
  '<' | '<=' | '>' | '>=' | '==' | '~=' | 
  'and' | 'or';

unop := '-' | 'not' | '#';